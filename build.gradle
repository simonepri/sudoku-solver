plugins {
  // Apply the java plugin to add support for Java.
  id 'java'

  // Apply the application plugin to add support for building an application.
  id 'application'

  // Apply the jacoco plugin to enable coverage generation for tests.
  id 'jacoco'

  // Apply the checkstyle plugin.
  id 'checkstyle'

  // Apply test logger plugin.
  id 'com.adarshr.test-logger' version '1.6.0'
}

repositories {
  // Use jcenter for resolving your dependencies.
  jcenter()
  // Use mavenCentral for resolving your dependencies.
  mavenCentral()
}

dependencies {
  // This dependency is found on compile classpath of this component and consumers.
  implementation 'com.google.guava:guava:26.0-jre'

  // Use JUnit test framework.
  testImplementation 'junit:junit:4.12'

  // Use AssertJ assert library.
  testCompile 'org.assertj:assertj-core:3.11.1'
}

test {
  // Log detailed errors.
  testLogging {
    exceptionFormat = 'full'
  }
  filter {
    // Exclude benchmark tests.
    excludeTestsMatching '*.*BenchTest'
  }
}

jacoco {}
jacocoTestReport {
  // Configure reports for jacoco plugin.
  reports {
    xml.enabled true
    html.enabled true
  }
}

checkstyle {
  showViolations = true
}
tasks.withType(Checkstyle) {
  // Configure reports for checkstyle plugin.
  reports {
    xml.enabled true
    html.enabled true
  }
}

testlogger {
  slowThreshold 0
}

task bench(type: Test) {
  group 'Verification'
  description 'Runs the benchmark tests'
  outputs.upToDateWhen { false }

  filter {
    includeTestsMatching '*.*BenchTest'
  }
}

// Execute jacocoTestReport on check.
check.dependsOn 'jacocoTestReport'

// Define the main class for the application.
mainClassName = 'sudoku.App'
