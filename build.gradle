plugins {
  // Apply the java plugin to add support for Java.
  id 'java'

  // Apply the application plugin to add support for building an application.
  id 'application'

  // Apply the jacoco plugin to enable coverage generation for tests.
  id 'jacoco'

  // Apply the checkstyle plugin.
  id 'checkstyle'

  // Apply test logger plugin.
  id 'com.adarshr.test-logger' version '1.6.0'
}

// Set the project version.
version = '1.0.0'

// Define the main class for the application.
mainClassName = 'sudoku.App'

repositories {
  // Use jcenter for resolving your dependencies.
  jcenter()
  // Use mavenCentral for resolving your dependencies.
  mavenCentral()
}

dependencies {
  // This dependency is found on compile classpath of this component and consumers.
  implementation 'com.google.guava:guava:26.0-jre'

  // Use JUnit test framework.
  testImplementation 'junit:junit:4.12'

  // Use AssertJ assert library.
  testCompile 'org.assertj:assertj-core:3.11.1'

  // Use JCommander cli framework.
  compile 'com.beust:jcommander:1.71'
}

test {
  // Log detailed errors.
  testLogging {
    exceptionFormat = 'full'
  }
  // Exclude benchmark tests.
  filter {
    excludeTestsMatching '*.*BenchTest'
  }
}

jar {
  // Configure the manifest fo the jar.
  manifest {
    attributes (
      'Implementation-Title': 'Sudoku Solutions Enumerator',
      'Implementation-Version': version,
      'Main-Class': mainClassName
    )
  }
  // Filename of the jar generated.
  baseName = project.name
}
task fatJar(type: Jar) {
  // Configure the manifest fo the jar.
  manifest {
    attributes (
      'Implementation-Title': 'Sudoku Solutions Enumerator',
      'Implementation-Version': version,
      'Main-Class': mainClassName
    )
  }
  // Filename of the jar generated.
  baseName = project.name + '-all'
  // Include dependencies in the jar.
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  // Add license to the jar.
  with jar
}
// Execute fatJar on assemble.
assemble.dependsOn 'fatJar'

jacoco {}
jacocoTestReport {
  // Configure reports for jacoco plugin.
  reports {
    xml.enabled true
    html.enabled true
  }
}
// Execute jacocoTestReport on check.
check.dependsOn 'jacocoTestReport'

checkstyle {
  showViolations = true
}
tasks.withType(Checkstyle) {
  // Configure reports for checkstyle plugin.
  reports {
    xml.enabled true
    html.enabled true
  }
}

testlogger {
  // Always show execution time for tests.
  slowThreshold 0
}

task bench(type: Test) {
  group 'Verification'
  description 'Runs the benchmark tests'
  outputs.upToDateWhen { false }

  filter {
    includeTestsMatching '*.*BenchTest'
  }
}

task s_bench(type: Test) {
  group 'Verification'
  description 'Runs the benchmark tests'
  outputs.upToDateWhen { false }

  filter {
    includeTestsMatching '*.*Sequential*BenchTest'
  }
}

task p_bench(type: Test) {
  group 'Verification'
  description 'Runs the benchmark tests'
  outputs.upToDateWhen { false }

  filter {
    includeTestsMatching '*.*Parallel*BenchTest'
  }
}
